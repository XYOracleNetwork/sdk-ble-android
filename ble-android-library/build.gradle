apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group = 'network.xyo'

buildscript {

    ext {
        experimentalsEnabled = [
                "-Xuse-experimental=kotlinx.coroutines.InternalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"
        ]
    }

    repositories {
        mavenCentral()
        mavenLocal()
        google()
    }
}

def majorVersion = 4
def minorVersion = 0
def patch = 41

def verString = majorVersion + '.' + minorVersion + '.' + patch

task printVersion {
    println verString
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        kotlinOptions {
            allWarningsAsErrors = true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        defaultPublishConfig 'release'
    }

    defaultPublishConfig "release"

    lintOptions {
        htmlReport false
        abortOnError true
        warningsAsErrors true
        disable "HardwareIds", "MissingPermission"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "4G"
    }

    packagingOptions {
        exclude 'META-INF/build.kotlin_module'
        exclude 'META-INF/atomicfu.kotlin_module'
        exclude 'META-INF/library_release.kotlin_module'
    }
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/ble-android-library-release.aar")
            groupId 'network.xyo'
            artifactId 'sdk-ble-android'
            version verString

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode()['dependencies'][0] ?: asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.github.XYOracleNetwork:sdk-base-android:4.2.16'
    implementation 'org.jetbrains:annotations:17.0.0'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.64'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.6.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'

    implementation "androidx.annotation:annotation:1.3.0"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all { task ->
    task.kotlinOptions.freeCompilerArgs += experimentalsEnabled
}